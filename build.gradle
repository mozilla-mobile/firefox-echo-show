// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.11'
    ext.coroutines_version = '1.1.0'

    ext.androidx_version = '1.0.0'
    ext.architecture_components_version = '2.0.1'
    ext.espresso_version = '3.1.1'
    ext.moz_components_version = '0.31.0'

    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        // Note that Gradle plugin version is also declared in buildSrc/build.gradle.kts
        // If your plugin isn't behaving the way you expect, verify that these two versions
        // are in sync.  Or even better, figure out a way to get rid of one of them.
        classpath 'com.android.tools.build:gradle:3.3.2'

        classpath 'org.ajoberstar:grgit:1.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // Mozilla Android gradle plugin. For details, see the repo:
        //   https://github.com/mozilla-mobile/android-automation-tools/tree/master/gradle-plugin
        classpath "org.mozilla.android:gradle-plugin:0.1"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC12"
}

detekt {
    toolVersion = "1.0.0-RC12"
    input = files("$projectDir")
    config = files("$projectDir/quality/detekt.yml")
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.29.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "**/*.kt"
}
